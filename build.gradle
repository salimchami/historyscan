import org.gradle.internal.os.OperatingSystem
plugins {
    id 'java'
    id 'org.springframework.boot' version '2.7.15'
    id 'io.spring.dependency-management' version '1.1.3'
}

group = 'io.sch'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

repositories {
    mavenCentral()
    maven { url 'https://repo1.maven.org/maven2/' }
}

tasks.register('checkClocInPath') {
    doLast {
        if (OperatingSystem.current().isUnix()) {
            // Pour Linux/Mac
            def result = exec {
                commandLine 'sh', '-c', 'command -v cloc'
                ignoreExitValue true
            }
            if (result.exitValue != 0) {
                throw new GradleException("CLOC not found in PATH.")
            }
        } else if (OperatingSystem.current().isWindows()) {
            // Pour Windows
            def result = exec {
                commandLine 'cmd', '/c', 'where cloc'
                ignoreExitValue true
            }
            if (result.exitValue != 0) {
                throw new GradleException("CLOC not found in PATH.")
            }
        } else {
            throw new GradleException("Unsupported operating system.")
        }
    }
}
build.dependsOn(checkClocInPath)

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-hateoas'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.eclipse.jgit:org.eclipse.jgit:6.7.0.202309050840-r'
    implementation 'org.eclipse.jgit:org.eclipse.jgit.ssh.apache:6.7.0.202309050840-r'
    implementation 'org.eclipse.jgit:org.eclipse.jgit.gpg.bc:6.7.0.202309050840-r'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
    useJUnitPlatform()
}

